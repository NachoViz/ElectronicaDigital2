; ELECTRÓNICA DIGITAL II
    
; --------  Proyecto-sensores  ---------
    
; Autor:
    ; Ignacio Vizgarra
    
; Profesor:
    ; Martin Del Barco
     
; Fecha: 24/05/2025

LIST    P=16F887
#include <P16F887.INC>
    
__CONFIG _CONFIG1, _FOSC_INTRC_NOCLKOUT & _WDTE_OFF & _PWRTE_OFF & _MCLRE_ON & _CP_OFF & _CPD_OFF & _BOREN_OFF & _IESO_OFF & _FCMEN_OFF & _LVP_OFF
__CONFIG _CONFIG2, _BOR4V_BOR40V & _WRT_OFF
    
    CBLOCK 0x20
    DCounter1
    DCounter2
    DCounter3
    DCounter4
    DCounter5
    DCounter6
    ENDC

#DEFINE START PORTA,0	    ;DEFINIMOS EL PIN DE ENTRADA PARA LA SEÑAL DEL SWITCH DE ENCENDIDO DEL SISTEMA
#DEFINE SOI PORTC,0	    ;DEFINIMOS EL PIN DE ENTRADA PARA LA SEÑAL DEL SENSOR OPTICO DE INICIO DEL RECORRIDO
#DEFINE SOL PORTC,1	    ;DEFINIMOS EL PIN DE ENTRADA PARA LA SEÑAL DEL SENSOR OPTICO DE FARO PARA LLENADO
#DEFINE SOF PORTC,2	    ;DEFINIMOS EL PIN DE ENTRADA PARA LA SEÑAL DEL SENSOR OPTICO DE FIN DEL RECORRIDO
#DEFINE CR PORTC,6	    ;DEFINIMOS EL PIN DE ENTRADA PARA LA SEÑAL DEL SENSOR DE COLOR ROJO
#DEFINE CA PORTC,7	    ;DEFINIMOS EL PIN DE ENTRADA PARA LA SEÑAL DEL SENSOR DE COLOR AZUL
#DEFINE SU_SA PORTE,1	    ;DEFINIMOS EL PIN DE ENTRADA PARA LA SEÑAL DEL SENSOR ULTRASONICO DE SUFICIENTE AGUA
#DEFINE SU_NMAX PORTE,2	    ;DEFINIMOS EL PIN DE ENTRADA PARA LA SEÑAL DEL SENSOR ULTRASONICO DE INSUFICIENTE AGUA
#DEFINE TLLENO PORTD,1	    ;DEFINIMOS EL PIN QUE INDICARÁ SI EL TANQUE ESTÁ AL MAXIMO INDICADO POR SU_NMAX
#DEFINE AIN_SUF PORTD,2	    ;DEFINIMOS EL PIN QUE INDICARÁ SI EL TANQUE ESTÁ CON AGUA INSUFICIENTE
#DEFINE M_P_M PORTD,3	    ;DEFINIMOS EL PIN DE MARCHA O PARO DE LA BANDA TRANSPORTADORA.
#DEFINE B_A PORTD,4	    ;DEFINIMOS EL PIN DE MARCHA O PARO DE LA BOMBA
#DEFINE SENSON PORTD,5	    ;DEFINIMOS EL PIN PARA MANDAR A ACTIVAR EL SENSADO DEL NIVEL SEL AGUA
    
    ORG	    0X00
    GOTO    INICIO
    ORG	    0X05
INICIO
;CONFIGURACIONES DE INICIO.
    BANKSEL ANSEL
    CLRF    ANSEL		; PONE EN 0 TODOS LOS BITS DEL REGISTRO ANSEL CONFIGURANDO EL PORTA/PORTE COMO DIGITALES
;BORR	CLRF	ANSELH		; PONE EN 0 TODOS LOS BITS DEL REGISTRO ANSELH CONFIGURANDO EL PORTB COMO DIGITALES
;BORR1	CLRF	TRISB		; PONE EN 0 TODOS LOS BITS DEL REGISTRO TRISB CONFIGURANDO EL PUERTO B COMO SALIDA

    BANKSEL TRISD
    CLRF    TRISD		; CONFIGURAMOS COMO SALIDA EL PUERTO D.
    
    BANKSEL PORTD
    CLRF    PORTD
PRINCIPAL
    BTFSS   START		; REVISAR LA ENTRADA DE START/RA0
    GOTO    PRINCIPAL	; VUELVE A REVISAR
    GOTO    REVISAR_NA	; SI ESTÁ EN 1 SALTARÁ A REVISAR EL NIVEL DEL AGUA.
REVISAR_NA
    BSF	    SENSON		; MANDAMOS A SENSAR EL NIVEL DEL AGUA
    CALL    RETARDO_500MS	; UN RETARDO PARA DAR TIEMPO QUE LLEGUE LA SEÑAL
    BCF	    SENSON		;
    BTFSS   SU_SA		; REVISAR SI HAY SUFICIENTE AGUA PARA CONTINUAR.
    CALL    NO_AGUA		; LLAMAMOS A UNA SUBRUTINA PARA CUANDO NO HAYA AGUA

REVISAR_SOF
    BTFSC	SOF		 ; REVISAMOS SI YA HAY ALGO EN EL FINAL DE LA BANDA SÍ HAY ALGO EN EL PIN HAY UN 0
    GOTO	REVISAR_SOI	 ;
    CALL	H_ALGOF		    ; SI HAY ALGO ENTONCES LLAMAMOS A LA SUBRUTINA DE QUE HAY ALGO
REVISAR_SOI
    BTFSC  	SOI		; REVISAMOS SI HAY ALGO EN EL PUNTO DE INICIO DEL RECORRIDO.
    GOTO	PRINCIPAL	  ; REGRESAMOS A PRINCIPAL EN CASO DE QUE NO HAYA NADA AL INICIO DE LA BANDA
    BSF		M_P_M		 ; SI HAY ALGO ENTONCES ENCENDEMOS LA BANDA TRANSPORTADORA

REVISAR_SOL
    BTFSC	SOL		; REVISAMOS SI YA LLEGO AL PUNTO DE LLENADO
    GOTO	REVISAR_SOL    ;
    CALL	H_ALGOL        ; YA LLEGÓ AL PUNTO DE LLENADO
    CALL	RETARDO_1S    ; UN RETARDO POR SI QUEDA UN POQUITO DE AGUA GOTEANDO
    BSF		M_P_M	      ; VOLVEMOS A PRENDER EL MOTOR
FINAL
    BTFSC	SOF	      ; REVISAR SI YA LLEGO AL FINAL DE LA BANDA
    GOTO	FINAL
    CALL	H_ALGOF        ; LLAMAMOS A LA SUBRUTINA DE QUE HAY ALGO AL FINAL DE LA BANDA
    CALL	RETARDO_1S	     ; ESPERAMOS UN SEGUNDO Y
    GOTO	PRINCIPAL	     ; REPETIMOS EL PROCESO.

;SUBRUTINA PARA CUANDO HAY ALGO EN EL PUNTO DE LLENADO DE LA BANDA;
H_ALGOL
    BCF     M_P_M       ; APAGAMOS EL MOTOR
    CALL    RETARDO_500MS ; UN RETARDO PARA QUE SE ESTABILICE EL RECIPIENTE
REVISAR_CR
    CALL    RETARDO_1S  ; PARA QUE LE DE TIEMPO DE RESIVIR LA SEÑAL DEL SENSOR
    BTFSS   CR          ; SI ESTÁ EN 1 ES COLOR ROJO
    GOTO    REVISAR_CA
    BSF     B_A         ; ENCENDEMOS LA BOMBA DE AGUA DURANTE LO QUE INDIQUE LA CANTIDAD DE LLAMADAS DE RETARDOS DE 1S
    CALL    RETARDO_1S
    CALL    RETARDO_1S
    CALL    RETARDO_1S
    CALL    RETARDO_1S
    BCF     B_A
    RETURN
REVISAR_CA
    BTFSS   CA          ; SI ESTÁ EN 1 ES COLOR AZUL
    GOTO    NO_COLOR    ; SI NO ES ROJO NI AZUL PUES SALTAMOS A NOCOLOR.
    BSF     B_A         ; ENCENDEMOS LA BOMBA DE AGUA DURANTE LO QUE INDIQUE LA CANTIDAD DE LLAMADAS DE RETARDOS DE 1S
    CALL    RETARDO_1S
    CALL    RETARDO_1S
    CALL    RETARDO_1S
    CALL    RETARDO_1S
    CALL    RETARDO_1S
    BCF     B_A
NO_COLOR
    RETURN

;SUBRUTINA PARA CUANDO HAY ALGO AL FINAL DE LA BANDA;
H_ALGOF
    BCF     M_P_M       ; ASEGURAMOS LA DETENCIÓN DE LA BANDA TRANSPORTADORA
    BTFSS   SOF         ; REVISAMOS SI SIGUE HABIENDO ALGO
    GOTO    H_ALGOF
    RETURN

;SUBRUTINA PARA CUANDO NO HAYA AGUA*******************************
NO_AGUA
    BCF     SENSON
    CALL    RETARDO_500MS
    BSF     AIN_SUF     ; ENCENDEMOS UN LED ROJO PARA CUANDO NO HAYA AGUA JUNTO A UN BUZZER CONECTADO A LA MISMA SALIDA (O SOLO UNA OP
    CALL    RETARDO_1S  ; DURANTE 1 SEGUNDO
    BCF     AIN_SUF     ; LO APAGAMOS
    CALL    RETARDO_1S  ; DURANTE 1 SEGUNDO
    BSF     AIN_SUF     ; Y REPETMOS
    CALL    RETARDO_1S
    BCF     AIN_SUF
    CALL    RETARDO_1S
    BSF     AIN_SUF
    CALL    RETARDO_1S
    BCF     AIN_SUF
    BSF     SENSON
    CALL    RETARDO_500MS
    BCF     SENSON
    CALL    RETARDO_500MS
    BTFSS   SU_NMAX     ; REVISAMOS SI EL AGUA ESTA EN MAX (1)
    GOTO    NO_MAX      ; SI NO SALTAMOS A NO_MAX PARA UN RETARDO Y QUE VUELVA A REPETIR LA ALARMA
    CALL    RETARDO_1S  ; SI YA ESTA EN MAX ENTONCES LLAMAMOS UN RETARDO POR SI LAS DUDAS
    RETURN              ; REGRESAMOS A LA SIG LINEA DE DONDE FUE LLAMADO
    
NO_MAX  
    CALL    RETARDO_1S
    GOTO    NO_AGUA ;

;SUBRUTINA DE RETARDO DE 1 SEGUNDO********************************
RETARDO_1S
    MOVLW   0xac
    MOVWF   DCounter1
    MOVLW   0x13
    MOVWF   DCounter2
    MOVLW   0x06
    MOVWF   DCounter3
LOOP
    DECFSZ  DCounter1, 1
    GOTO    LOOP
    DECFSZ  DCounter2, 1
    GOTO    LOOP
    DECFSZ  DCounter3, 1
    GOTO    LOOP
    NOP
    RETURN
;SUBRUTINA DE RETARDO DE 500 MILI SEGUNDOS************************
RETARDO_500MS
    MOVLW   0x54
    MOVWF   DCounter4
    MOVLW   0x8a
    MOVWF   DCounter5
    MOVLW   0x03
    MOVWF   DCounter6
LOOP1
    DECFSZ  DCounter4, 1
    GOTO    LOOP1
    DECFSZ  DCounter5, 1
    GOTO    LOOP1
    DECFSZ  DCounter6, 1
    GOTO    LOOP1
    NOP
    RETURN

    END