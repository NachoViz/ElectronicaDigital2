; TP2 - ELECTRÓNICA DIGITAL II
    
; Autores:
    ; Ignacio Vizgarra
    ; Tomás Figueroa
    ; Julieta Pérez Echeverría
    
; Profesor:
    ; Martin Del Barco
    
; Fecha: 21/05/2025

     LIST    P=16F887
     #include <P16F887.INC>
    
__CONFIG _CONFIG1, _FOSC_INTRC_NOCLKOUT & _WDTE_OFF & _PWRTE_OFF & _MCLRE_ON & _CP_OFF & _CPD_OFF & _BOREN_OFF & _IESO_OFF & _FCMEN_OFF & _LVP_OFF
__CONFIG _CONFIG2, _BOR4V_BOR40V & _WRT_OFF

         CBLOCK 0x20
	 W_TEMP
	 STATUS_TEMP
	 BANDERAS
	 CONT_DE_DIS
	 ACTIVAR_PORT
	 CONTADOR250
	 ACTIVADOR
	 ENDC
	 
	 CBLOCK 0x30
	 SEG1
	 SEG2
	 MIN1
	 MIN2
	 HORA1
	 HORA2
	 ENDC
	 
         ORG 0x00
         GOTO INICIO
     
         ORG 0x04
         GOTO SRI
     
         ORG 0x05
INICIO
	 CALL INICIALIZACION
	 
LOOP	 
	 BTFSC BANDERAS,2 
	 CALL STOP
	 BTFSC BANDERAS, 0
	 CALL CONTAR
	 BTFSC BANDERAS, 1
	 CALL MOSTRAR_HORA
	 GOTO LOOP
	 
INICIALIZACION
	 ; CONFIGURACIÓN DE LOS PUERTOS
	 BANKSEL ANSELH
	 CLRF ANSELH
	 CLRF ANSEL
	 BANKSEL TRISD
	 CLRF TRISD
	 CLRF TRISA
	 BSF TRISB, 0
	 BSF TRISB, 1
	 
	 ; CONFIGURACIÓN DE LAS INTERRUPCIONES
	 BANKSEL OPTION_REG
	 ; RB0
	 BCF OPTION_REG, 7
	 BSF WPUB, 0
	 BCF OPTION_REG, INTEDG
	 BSF INTCON, INTE
	 ; TMR0
	  BSF INTCON, T0IE
	  BCF OPTION_REG, T0CS
	  BCF OPTION_REG, PSA
	  BSF OPTION_REG, PS2
	  BCF OPTION_REG, PS1
	  BCF OPTION_REG, PS0
	  BANKSEL PORTD
	  MOVLW .131
	  MOVWF TMR0
	  ; RB1
	  BANKSEL IOCB
	  BSF INTCON, RBIE
	  BSF IOCB, 1
	 
	 ; HABILITO INTERRUPCIONES
	 BSF INTCON, GIE
	 
	 ; INICIO LAS VARIABLES CON LAS QUE VOY A TRABAJAR
	 BANKSEL PORTD
	 CLRF W_TEMP
	 CLRF STATUS_TEMP
	 CLRF SEG1
	 CLRF SEG2
	 CLRF MIN1
	 CLRF MIN2
	 CLRF HORA1
	 CLRF HORA2
	 CLRF BANDERAS
	 CLRF CONT_DE_DIS
	 MOVLW 0x7F
	 MOVWF ACTIVAR_PORT
	 MOVLW .250
	 MOVWF CONTADOR250 
	 
	 CLRF ACTIVADOR
	 INCF ACTIVADOR, F
	 
	 MOVLW 0xFF
	 MOVWF PORTA
	 
	 RETURN

	
SRI
	 ; GUARDO CONTEXTO
	 MOVWF W_TEMP
	 SWAPF STATUS, W
	 MOVWF STATUS_TEMP
	 
	 ;TESTEO DE DÓNDE VINO LA INTERRPCIÓN Y DERIVO
	 BTFSC INTCON, RBIF
	 CALL SUBRRUTINA_RB1
	 BTFSC INTCON, INTF
	 CALL SUBRRUTINA_RB0
	 BTFSC INTCON, T0IF
	 CALL SUBRRUTINA_TMR0
	 
	 ; RECUPERO CONTEXTO
	 SWAPF STATUS_TEMP, W
	 MOVWF STATUS
	 SWAPF W_TEMP, F
	 SWAPF W_TEMP, W
	 RETFIE
	 
SUBRRUTINA_RB1
	 BCF INTCON, RBIF
	 BTFSC PORTB, RB1
	 RETURN
	 BSF BANDERAS, 2
	 COMF ACTIVADOR, F
	 RETURN
	 
SUBRRUTINA_RB0
	 BCF INTCON, INTF
	 BSF BANDERAS, 0
	 RETURN
	 
SUBRRUTINA_TMR0
	 BCF INTCON, T0IF
	 MOVLW .131
	 MOVWF TMR0
	 BSF BANDERAS, 1
	 INCF CONT_DE_DIS, F
	 RLF ACTIVAR_PORT, F
	 MOVLW .7
	 SUBWF CONT_DE_DIS, W
	 BTFSS STATUS, Z
	 RETURN
	 CLRF CONT_DE_DIS
	 MOVLW 0x7F
	 MOVWF ACTIVAR_PORT
	 RETURN
	 	
STOP
	 BTFSC ACTIVADOR, 0
	 BSF INTCON, INTE
	 BCF INTCON, INTE
	 RETURN
	 
CONTAR  
	; ACTUALIZA EL VALOR DEL CRONÓMETRO ALMACENADO EN LOS 6 REGISTROS (SEG1, SEG2, MIN1, MIN2, HORA1, HORA2)
	 BCF BANDERAS, 0  
	; Segundos1
	 INCF SEG1, F
	 MOVLW .10
	 SUBWF SEG1, W
	 BTFSS STATUS, Z
	 RETURN
	 CLRF SEG1
	 ; Segundos2
	 INCF SEG2, F
	 MOVLW .6
	 SUBWF SEG2, W
	 BTFSS STATUS, Z
	 RETURN
	 CLRF SEG2
	 ; Minutos1
	 INCF MIN1, F
	 MOVLW .10
	 SUBWF MIN1, W
	 BTFSS STATUS, Z
	 RETURN
	 CLRF MIN1
	 ; Minutos2
	 INCF MIN2, F
	 MOVLW .6
	 SUBWF MIN2, W
	 BTFSS STATUS, Z
	 RETURN
	 CLRF MIN2
	 ; Horas1
	 INCF HORA1, F
	 MOVLW .10
	 SUBWF HORA1, W
	 BTFSS STATUS, Z
	 RETURN
	 CLRF HORA1
	 ; Horas2
	 INCF HORA2, F
	 MOVLW .6
	 SUBWF HORA2, W
	 BTFSS STATUS, Z
	 RETURN
	 CLRF HORA2
	 RETURN
	 

MOSTRAR_HORA
	 MOVLW 0xFF
	 MOVWF PORTA
	 BCF BANDERAS, 1
	 MOVLW 0x2F
	 MOVWF FSR
         MOVF CONT_DE_DIS, W
	 ADDWF FSR, F
	 MOVF INDF, W
	 CALL TABLA7SEG
	 MOVWF PORTD
	 
	 MOVF CONT_DE_DIS, W
	 CALL TABLA_SELECTORA
	 MOVWF PORTA
	 RETURN
	 
TABLA7SEG 
	 ; ÁNODO COMÚN
         ADDWF PCL, F
         RETLW 0xC0   ; 0 ? 11000000
         RETLW 0xF9   ; 1 ? 11111001
         RETLW 0xA4   ; 2 ? 10100100
         RETLW 0xB0   ; 3 ? 10110000
         RETLW 0x99   ; 4 ? 10011001
         RETLW 0x92   ; 5 ? 10010010
         RETLW 0x82   ; 6 ? 10000010
         RETLW 0xF8   ; 7 ? 11111000
         RETLW 0x80   ; 8 ? 10000000
         RETLW 0x90   ; 9 ? 10010000
	 

TABLA_SELECTORA
         ADDWF PCL, F
         RETLW 0xFF
         RETLW 0xFE
         RETLW 0xFD
         RETLW 0xFB
         RETLW 0xF7
         RETLW 0xEF
         RETLW 0xDF
         RETLW 0xBF
         RETLW 0x7F

	 END